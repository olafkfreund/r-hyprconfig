# Maintainer: olafkfreund <olafkfreund@gmail.com>
pkgname=r-hyprconfig-git
pkgver=r1.0.6.r0.gbbde04c
pkgrel=1
pkgdesc="A modern TUI for visually configuring Hyprland (git version)"
arch=('x86_64' 'aarch64')
url="https://github.com/olafkfreund/r-hyprconfig"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('rust' 'cargo' 'git')
provides=('r-hyprconfig')
conflicts=('r-hyprconfig')
source=("git+https://github.com/olafkfreund/r-hyprconfig.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "${pkgname%-git}"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/r-hyprconfig"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}